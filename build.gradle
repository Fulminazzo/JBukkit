plugins {
    id 'java-library'
    id 'maven-publish'
    id 'io.freefair.lombok' version "8.4"
}

group = 'it.fulminazzo'
version = '2.2'

allprojects {
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'
    apply plugin: 'io.freefair.lombok'

    group = "${rootProject.group}"
    version = "${rootProject.version}"

    this.ext.getParentFromProject = { project ->
        return new HashMap<>(project.getProperties()).get("parent")
    }

    this.ext.getProjectGroupId = {
        String groupId = ""
        Project tmp = parent
        while (tmp != null) {
            groupId = "${tmp.name}.${groupId}"
            tmp = getParentFromProject(tmp)
        }
        if (groupId.size() > 0)
            groupId = "." + groupId.substring(0, groupId.length() - 1)
        return "${rootProject.group}${groupId}"
    }

    this.ext.getSpigotVersion = {
        if (project.name != "common") {
            if (project.name == "obsolete")
                return 'org.spigotmc:spigot-api:1.8.8-R0.1-SNAPSHOT'
            else return 'org.spigotmc:spigot-api:1.21.4-R0.1-SNAPSHOT'
        } else return 'org.spigotmc:spigot-api:1.20.2-R0.1-SNAPSHOT'
    }

    repositories {
        mavenCentral()
        maven { url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
        maven { url = 'https://oss.sonatype.org/content/repositories/snapshots' }
        maven { url = 'https://oss.sonatype.org/content/repositories/central' }
        maven { url = 'https://repo.fulminazzo.it/releases/' }
    }

    dependencies {
        compileOnly 'org.projectlombok:lombok:1.18.32'
        annotationProcessor 'org.projectlombok:lombok:1.18.32'
        compileOnly 'org.jetbrains:annotations:24.1.0'
        annotationProcessor 'org.jetbrains:annotations:24.1.0'

        api 'it.fulminazzo:FulmiCollection:1.4.2'
        implementation 'org.mockito:mockito-core:4.11.0'

        if (!project.name.equals('common')) api project(':common')
        String spigotVersion = getSpigotVersion()
        compileOnly spigotVersion
        testImplementation spigotVersion

        compileOnly platform('org.junit:junit-bom:5.9.1')
        compileOnly 'org.junit.jupiter:junit-jupiter'

        testImplementation platform('org.junit:junit-bom:5.9.1')
        testImplementation 'org.junit.jupiter:junit-jupiter'
    }

    test {
        useJUnitPlatform()
    }

    tasks.register('sourcesJar', Jar) {
        from sourceSets.main.allSource
        archiveClassifier = 'sources'
    }

    tasks.register('javadocJar', Jar) {
        from javadoc
        archiveClassifier = 'javadoc'
    }

    publishing {
        publications {
            maven(MavenPublication) {
                groupId = getProjectGroupId()
                artifactId = "${project.name}"
                version = "${rootProject.version}"

                from components.java
                artifact sourcesJar
                artifact javadocJar
            }
        }

        repositories {
            maven {
                url "https://repo.fulminazzo.it/releases"
                credentials {
                    username = System.getenv("REPO_USERNAME")
                    password = System.getenv("REPO_PASSWORD")
                }
                authentication {
                    basic(BasicAuthentication)
                }
            }
        }
    }
}